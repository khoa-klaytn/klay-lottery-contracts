// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;
pragma abicoder v2;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "./IndexedSSLottery.sol";
import "./interfaces/ISSLottery.sol";
import "./interfaces/IVRFConsumer.sol";

contract SSLottery is IndexedSSLottery {
    using SafeERC20 for IERC20;

    address payable public injectorAddress;

    modifier onlyOwnerOrInjector() {
        require((msg.sender == owner()) || (msg.sender == injectorAddress), "Not owner or injector");
        _;
    }

    event LotteryInjection(uint256 indexed lotteryId, uint256 injectedAmount);

    event NewOperatorAndInjectorAddresses(address operator, address injector);
    event NewRandomGenerator(address indexed vrfConsumer);
    event AdminTokenRecovery(address token, uint256 amount);

    constructor(
        address _vrfConsumerAddress,
        address _dataFeedConsumerAddress,
        uint256 _minTicketPriceInUsd
    ) IndexedSSLottery(_vrfConsumerAddress, _dataFeedConsumerAddress, _minTicketPriceInUsd) {}

    /**
     * @notice Inject funds
     * @param _lotteryId: lottery id
     * @dev Callable by owner or injector address
     */
    function injectFunds(uint256 _lotteryId) external payable onlyOwnerOrInjector {
        requireOpen(_lotteryId);

        uint256 amount = msg.value;
        _lotteries[_lotteryId].amountCollected += amount;

        emit LotteryInjection(_lotteryId, amount);
    }

    /**
     * @notice Change the random generator
     * @dev The calls to functions are used to verify the new generator implements them properly.
     * It is necessary to wait for the VRF response before starting a round.
     * Callable only by the contract owner
     * @param _vrfConsumerAddress: address of the random generator
     */
    function changeRandomGenerator(address _vrfConsumerAddress) external onlyOwner {
        if (currentLotteryId != 0) {
            requireClaimable(currentLotteryId);
        }

        // Request a random number from the generator based on a seed
        IVRFConsumer(_vrfConsumerAddress).requestRandomNumberDirect();

        // Calculate the finalNumber based on the randomResult generated by ChainLink's fallback
        IVRFConsumer(_vrfConsumerAddress).randomResult();

        vrfConsumer = IVRFConsumer(_vrfConsumerAddress);

        emit NewRandomGenerator(_vrfConsumerAddress);
    }

    /**
     * @notice It allows the admin to recover wrong tokens sent to the contract
     * @param _tokenAddress: the address of the token to withdraw
     * @param _tokenAmount: the number of token amount to withdraw
     * @dev Only callable by owner.
     */
    function recoverWrongTokens(address _tokenAddress, uint256 _tokenAmount) external onlyOwner {
        IERC20(_tokenAddress).safeTransfer(address(msg.sender), _tokenAmount);
        emit AdminTokenRecovery(_tokenAddress, _tokenAmount);
    }

    /**
     * @notice Set max number of tickets
     * @dev Only callable by owner
     */
    function setMaxNumberTicketsPerBuy(uint256 _maxNumberTicketsPerBuy) external onlyOwner {
        require(_maxNumberTicketsPerBuy != 0, "Must be > 0");
        maxNumberTicketsPerBuyOrClaim = _maxNumberTicketsPerBuy;
    }

    /**
     * @notice Set operator and injector addresses
     * @dev Only callable by owner
     * @param _operatorAddress: address of the operator
     * @param _injectorAddress: address of the injector
     */
    function setOperatorAndInjectorAddresses(address _operatorAddress, address _injectorAddress) external onlyOwner {
        operatorAddress = payable(_operatorAddress);
        injectorAddress = payable(_injectorAddress);

        emit NewOperatorAndInjectorAddresses(_operatorAddress, _injectorAddress);
    }

    /**
     * @notice Calculate price of a set of tickets
     * @param _discountDivisor: divisor for the discount
     * @param _ticketPrice price of a ticket
     * @param _numberTickets number of tickets to buy
     */
    function calculateTotalPriceForBulkTickets(
        uint256 _discountDivisor,
        uint256 _ticketPrice,
        uint256 _numberTickets
    ) public pure returns (uint256) {
        require(_discountDivisor >= MIN_DISCOUNT_DIVISOR, "Must be >= MIN_DISCOUNT_DIVISOR");
        require(_numberTickets != 0, "Number of tickets must be > 0");

        return _calculateTotalPriceForBulkTickets(_discountDivisor, _ticketPrice, _numberTickets);
    }

    function calculateCurrentTotalPriceForBulkTickets(uint256 _numberTickets) public view returns (uint256) {
        return
            calculateTotalPriceForBulkTickets(
                _lotteries[currentLotteryId].discountDivisor,
                _lotteries[currentLotteryId].ticketPrice,
                _numberTickets
            );
    }

    /**
     * @notice View lottery information
     * @param _lotteryId: lottery id
     */
    function viewLottery(uint256 _lotteryId) external view returns (Lottery memory) {
        return _lotteries[_lotteryId];
    }

    /**
     * @notice View ticker statuses and numbers for an array of ticket ids
     * @param _ticketIds: array of _ticketId
     */
    function viewNumbersAndStatusesForTicketIds(
        uint256[] calldata _ticketIds
    ) external view returns (uint32[] memory, bool[] memory) {
        uint256 length = _ticketIds.length;
        uint32[] memory ticketNumbers = new uint32[](length);
        bool[] memory ticketStatuses = new bool[](length);

        for (uint256 i = 0; i < length; i++) {
            ticketNumbers[i] = _tickets[_ticketIds[i]].number;
            if (_tickets[_ticketIds[i]].owner == address(0)) {
                ticketStatuses[i] = true;
            } else {
                ticketStatuses[i] = false;
            }
        }

        return (ticketNumbers, ticketStatuses);
    }

    /**
     * @notice View rewards for a given ticket, providing a bracket, and lottery id
     * @dev Computations are mostly offchain. This is used to verify a ticket!
     * @param _lotteryId: lottery id
     * @param _ticketId: ticket id
     */
    function viewRewardsForTicketId(uint256 _lotteryId, uint256 _ticketId) external view returns (uint256) {
        // Check lottery is in claimable status
        requireClaimable(_lotteryId);

        // Check ticketId is within range
        requireValidTicketId(_lotteryId, _ticketId);

        return _calculateRewardsForTicketId(_lotteryId, _ticketId);
    }

    /**
     * @notice View user ticket ids, numbers, and statuses of user for a given lottery
     * @param _user: user address
     * @param _lotteryId: lottery id
     * @param _cursor: cursor to start where to retrieve the tickets
     * @param _size: the number of tickets to retrieve
     */
    function viewUserInfoForLotteryId(
        address _user,
        uint256 _lotteryId,
        uint256 _cursor,
        uint256 _size
    ) external view returns (uint256[] memory, uint32[] memory, bool[] memory, uint256) {
        uint256 length = _size;
        uint256 numberTicketsBoughtAtLotteryId = _userTicketIdsPerLotteryId[_user][_lotteryId].length;

        if (length > (numberTicketsBoughtAtLotteryId - _cursor)) {
            length = numberTicketsBoughtAtLotteryId - _cursor;
        }

        uint256[] memory lotteryTicketIds = new uint256[](length);
        uint32[] memory ticketNumbers = new uint32[](length);
        bool[] memory ticketStatuses = new bool[](length);

        for (uint256 i = 0; i < length; i++) {
            lotteryTicketIds[i] = _userTicketIdsPerLotteryId[_user][_lotteryId][i + _cursor];
            ticketNumbers[i] = _tickets[lotteryTicketIds[i]].number;

            // True = ticket claimed
            if (_tickets[lotteryTicketIds[i]].owner == address(0)) {
                ticketStatuses[i] = true;
            } else {
                // ticket not claimed (includes the ones that cannot be claimed)
                ticketStatuses[i] = false;
            }
        }

        return (lotteryTicketIds, ticketNumbers, ticketStatuses, _cursor + length);
    }
}
